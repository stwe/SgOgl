#version 430

// splatmap.comp

layout (local_size_x = 16, local_size_y = 16) in;

layout (binding = 0, rgba32f) uniform writeonly image2D splatmap;

uniform sampler2D normalmap;
uniform int heightmapWidth;

void main()
{
    ivec2 xy = ivec2(gl_GlobalInvocationID.xy);
    vec2 texCoord = gl_GlobalInvocationID.xy / float(heightmapWidth);

    vec3 normalizedNormal = normalize(texture(normalmap, texCoord).rgb);

    vec4 color = vec4(0.0, 0.0, 0.0, 0.0);

    float slope = normalizedNormal.z;

    if (slope > 0.82)
    {
        color.x = 1.0; // r -> Sand or snow
    }
    else if (slope > 0.62)
    {
        color.y = 1.0; // g -> grass
    }
    else if (slope > 0.32)
    {
        color.z = 1.0; // b -> rock
    }
    else
    {
        color.w = 1.0; // a -> snow
    }

    imageStore(splatmap, xy, color);
}
